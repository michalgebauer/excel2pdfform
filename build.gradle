plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'org.beryx.runtime' version '1.1.3'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

repositories {
    jcenter()
}

ext {
    mainClassName = 'sk.itent.excel2pdfform.Application'
    poiVersion = '4.1.2'
    itextVersion = '7.1.12'
}

dependencies {
    compile group: 'org.apache.poi', name: 'poi', version: poiVersion
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: poiVersion
    compile group: 'com.itextpdf', name: 'itext7-core', version: itextVersion
    compile group: 'com.itextpdf', name: 'forms', version: itextVersion

    // Use the latest Groovy version for Spock testing
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.10'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.12'
}

application {
    mainClassName = project.ext.mainClassName
}

jar {
    manifest {
        attributes 'Implementation-Title': "excel2pdfform",
                'Main-Class': project.ext.mainClassName
    }
}

runtime {
    modules = [
            'jdk.charsets',
            'java.base',
            'java.desktop',
            'java.logging',
            'java.naming',
            'java.security.jgss',
            'java.sql',
            'java.xml',
            'java.xml.crypto']
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

tasks.runtime.doLast {
    copy {
        from file('src/main/resources/excel2pdfform')
        into file("$buildDir/image")
    }

    copy {
        from file('files')
        into file("$buildDir/image/files")
    }
}